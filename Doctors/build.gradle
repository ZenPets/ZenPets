apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def keystorePropertiesFile = file('keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

/** THE VERSIONING CODES **/
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 3
ext.versionClassifier = null
ext.isSnapshot = false
ext.minimumSdkVersion = 16

android {
    compileSdkVersion 27
    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        applicationId "co.zenpets.doctors"
        minSdkVersion 16
        targetSdkVersion 27
        multiDexEnabled true
        versionCode generateVersionCode()
        versionName generateVersionName()
//        versionCode 20180214
//        versionName "0.0.4"

        vectorDrawables {
            useSupportLibrary = true
        }
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        jumboMode = true
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName;
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /** MULTIDEX **/
    implementation 'com.android.support:multidex:1.0.3'

    /** CONSTRAINT LAYOUT **/
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    /** ANDROID SUPPORT **/
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    /** GOOGLE PLAY SERVICES **/
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-places:16.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    /** FIREBASE **/
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-storage:16.0.4'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation ('com.crashlytics.sdk.android:crashlytics:2.9.6@aar') {
        transitive = true
    }

    /** RETROFIT AND GSON **/
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'

    /** RAZOR PAY SDK **/
    implementation 'com.razorpay:checkout:1.4.7'

    /** FRESCO **/
    implementation 'com.facebook.fresco:fresco:1.9.0'
    implementation 'me.relex:photodraweeview:1.1.3'

    /** PICASSO **/
    implementation 'com.squareup.picasso:picasso:2.5.2'

    /** GLIDE **/
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    /** THE CIRCLE IMAGE VIEW LIBRARY **/
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    /** EASY IMAGE PICKER **/
    implementation 'com.github.jkwiecien:EasyImage:2.0.4'

    /** BUTTER KNIFE **/
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    /** MATERIAL DIALOGS **/
    implementation ('com.afollestad.material-dialogs:core:0.9.4.7') {
        exclude group: 'com.android.support'
    }

    /** MATERIAL DATE PICKER **/
    implementation ('com.wdullaer:materialdatetimepicker:3.3.1') {
        exclude group: 'com.android.support'
    }

    /** THE ICONICS LIBRARY **/
    implementation "com.mikepenz:iconics-core:2.9.3@aar"
    implementation "com.mikepenz:iconics-views:2.9.3@aar"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.4@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.0.13.0@aar'
    implementation 'com.mikepenz:weather-icons-typeface:2.0.10.4@aar'
    implementation 'com.mikepenz:ionicons-typeface:2.0.1.4@aar'
    implementation 'com.mikepenz:community-material-typeface:2.0.46.1@aar'
    implementation 'com.mikepenz:octicons-typeface:3.2.0.4@aar'
    implementation 'com.mikepenz:foundation-icons-typeface:3.0.0.4@aar'

    /** JODA TIME LIBRARY **/
    implementation 'joda-time:joda-time:2.10.1'

    /** IMAGE COMPRESSOR **/
    implementation 'id.zelory:compressor:2.1.0'

    /** MATERIAL CALENDAR VIEW **/
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'

    /** PRETTY TIME **/
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'

    /** TIMELINE VIEW **/
    implementation 'com.github.vipulasri:timelineview:1.0.6'
}

apply plugin: 'com.google.gms.google-services'